plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.9'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'checkstyle'
}

group = 'com.webtoonchat'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('checkstyle', Checkstyle) {
    description 'Check code convention'
    group 'verification'
    configFile file("${project.rootDir}/.idea/check-style/naver-checkstyle-rules.xml")
    showViolations true
    classpath = files()
    ignoreFailures = false
    source "src/main/java"
}

tasks.register('copyGitHooks', Copy) {
    from("${rootDir}/git-hooks/") {
        include '**/*'
        rename '(.*)', '$1'
    }
    into "${rootDir}/.git/hooks"
}

tasks.register('installGitHooks', Exec) {
    group 'git hooks'
    workingDir rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
}